import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

compileJava {
    sourceCompatibility = '17'
    targetCompatibility = '17'
    options.encoding = 'UTF-8'
    jar.enabled = false
}

group 'com.imnotstable.qualityeconomy'
version '1.5.1'

allprojects {
    apply plugin: 'java'
    apply plugin: 'com.github.johnrengelman.shadow'
    compileJava.sourceCompatibility = '17'
    compileJava.targetCompatibility = '17'
    compileJava.options.encoding = 'UTF-8'
    jar.enabled = false

    repositories {
        mavenCentral()
        maven { url 'https://repo.papermc.io/repository/maven-public/' }
        maven { url 'https://oss.sonatype.org/content/groups/public/' }
        maven { url 'https://jitpack.io' }
        maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
        maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    }

}

subprojects {

    dependencies {
        compileOnly 'org.projectlombok:lombok:1.18.32'
        annotationProcessor 'org.projectlombok:lombok:1.18.32'
        compileOnly 'org.jetbrains:annotations:24.1.0'
        annotationProcessor 'org.jetbrains:annotations:24.1.0'
    }

    shadowJar {
        exclude 'META-INF/**', 'LICENSE', 'module-info.class'
        from '../LICENSE'
        destinationDirectory = file('../libs')
    }

    build {
        dependsOn shadowJar
    }

    processResources {
        filter(ReplaceTokens, tokens: [version: rootProject.version])
    }

}