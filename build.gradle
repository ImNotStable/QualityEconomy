import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'io.github.goooler.shadow' version '8.1.8'
    id 'java-library'
}

group 'com.imnotstable.qualityeconomy'
version '1.5.4'

allprojects {
    compileJava.sourceCompatibility = '21'
    compileJava.targetCompatibility = '21'
    compileJava.options.encoding = 'UTF-8'
    jar.enabled = false
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'io.github.goooler.shadow'
    apply plugin: 'java-library'

    dependencies {
        compileOnly 'org.projectlombok:lombok:1.18.34'
        annotationProcessor 'org.projectlombok:lombok:1.18.34'
        compileOnly 'org.jetbrains:annotations:24.1.0'
        annotationProcessor 'org.jetbrains:annotations:24.1.0'
    }

    shadowJar {
        minimize()
        exclude 'META-INF/**', 'LICENSE', 'module-info.class'
        archiveFileName = "QualityEconomy-${rootProject.version}-${project.name.capitalize()}.jar"
        destinationDirectory = file('../libs')
    }

    build {
        dependsOn shadowJar
    }

    processResources {
        filter(ReplaceTokens, tokens: [version: rootProject.version])
    }

}